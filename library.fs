: 1+ s` +` ;
: 1- s` -` ;

: + s` [<+>-]<` ;
: - s` [<->-]<` ;
: * s` <[>[>+>+<<-]>>[<<+>>-]<<<-]>[-]>[<<+>>-]<<` ;

: dup  s` [>+>+<<-]>>[<<+>>-]<` ;
: drop s` [-]<` ;
: nip  s` <[-]>[<+>-]<` ;
: over s` <[>>+>+<<<-]>>>[<<<+>>>-]<` ;
: tuck s` [>+>+<<-]<[>+<-]>>>[<<<+>>>-]<` ;
: swap s` [>+<-]<[>+<-]>>[<<+>>-]<` ;
: rot  s` <<[>>>+<<<-]>[<+>-]>[<+>-]>[<+>-]<` ;
: -rot s` [>+<-]<[>+<-]<[>+<-]>>>[<<<+>>>-]<` ;

: emit   s` .` drop ;
: key  0 s` ,` ;

: 0=  s` >-<[>+<[-]]>[<->+]<` ;
: 0<>    s` [>-<[-]]>[<->+]<` ;
: =  - 0=  ;
: <> - 0<> ;

: /mod
    over
    if
        \ http://sampi.hatenablog.com/entry/2013/10/01/001136
        s` [>>+<<-]>+>>>>>+[<<<<[->+>+<<]>[-<+>]>[-<<<<<-[>]>>>>>>>-<<<<<<[<]>>>>]>+>]<<<<<<<[>]>[>[-<<<+>>>]>>>-<<<<<]>->[-]>>>>>[-]<<[<<<<<+>>>>>-]<<<<<`
    else
        s` [-]`
    then
;
: /   /mod nip  ;
: mod /mod drop ;

: max
    \ http://sampi.hatenablog.com/entry/2013/09/28/115426
    s` [>>+<<-]>+>>>+>>++[<<<<<<<-[>]>>>>>>>-<<<<<<[<]>>-[>]>>>>-<<<[<]>>+>]>[-]<<<-<<[-]<-<<[-]>>>>>>[<<<<<<+>>>>>>-]<<<<<<`
;
: min
    s` [>>+<<-]>+>>>+>>+[<<<<<<<-[>]>>>>>>>[-]<<<<<<[<]>>-[>]>>>>[-]<<<[<]>>+>]<<-<<[-]<->>>>[<<<<<<+>>>>>>-]<<<<<<`
;

: cr 10 emit ;
: bl 32 ;
: space  bl emit ;
: spaces bl s` <[>.<-]>[-]<<` ;

: . ( n -- )
    100 /mod
    dup if
        48 + emit
        1
    else
        drop
        0
    then
    swap
    10 /mod
    dup if
        48 + emit
        nip
    else
        drop
        swap if
            48 emit
        then
    then
    48 + emit
    space
;

: > ( a b -- bool )
    begin dup while
        over
        if
            swap 1- swap
        then
        1-
    repeat
    drop
    0<>
;
: < swap > ;
: <= > 0= ;
: >= swap <= ;
